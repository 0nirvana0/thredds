// The jacoco plugin adds the jacocoTestReport task, but only if the java plugin is already applied.
apply plugin: "jacoco"

tasks.withType(Test) {
    // Add the execution data that Jacoco generates as a task output.
    outputs.file jacoco.destinationFile
}

gradle.projectsEvaluated {
    // Will apply to "jacocoTestReport", ":it:integrationTestReport", ":jacocoRootReport", etc.
    tasks.withType(JacocoReport) {
        // Include all Test tasks from this project and any subprojects.
        dependsOn allprojects*.tasks*.withType(Test)

        // Source sets are added by the java plugin, and not all subprojects have that applied. ":dap4" is one.
        Collection<Project> projectsWithSourceSets = rootProject.subprojects.findAll { it.hasProperty("sourceSets") }
        List<SourceSet> mainSourceSets = projectsWithSourceSets*.sourceSets*.main

        sourceDirectories = files(mainSourceSets*.allSource*.srcDirs)
        classDirectories = files(mainSourceSets*.output)

        reports {
            xml.enabled = true
            html.enabled = true
            csv.enabled = false
        }
    }
}
