dependencies {
    compile project(':udunits')
    compile project(':httpservices')

    compile libraries["httpcore"]
    compile libraries["joda-time"]
    compile libraries["jdom2"]
    compile libraries["jcip-annotations"]
    compile libraries["quartz"]
    compile libraries["protobuf-java"]
    compile libraries["guava"]
    compile libraries["bzip2"]
    compile libraries["jcommander"]

    compile libraries["slf4j-api"]
    testRuntime libraries["slf4j-jdk14"]
}

sourceSets {
    test {
        java {
            exclude 'thredds/catalog2/**/*.java'
            // Causes JUnit to throw an exception: "No tests found in ucar.nc2.util.UnitTestCommon"
            exclude 'ucar/nc2/util/UnitTestCommon.java'
        }
    }

    integrationTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test, dependsOn: jar) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath
}

jar {
    manifest {
        attributes 'Implementation-Title': 'cdm'
    }
}

javadoc {
    title = 'Public javadoc for netcdf-java version ' + version
    source = fileTree(dir: 'src/main/java').matching {
        include 'thredds/catalog/*.java'
        include 'ucar/ma2/*.java'
        include 'ucar/nc2/*.java'
        include 'ucar/nc2/constants/*.java'
        include 'ucar/nc2/dataset/**/*.java'
        include 'ucar/nc2/dt/*.java'
        include 'ucar/nc2/dt/grid/*.java'
        include 'ucar/nc2/ft/*.java'
        include 'ucar/nc2/ncml/*.java'
        include 'ucar/nc2/time/*.java'
        include 'ucar/nc2/units/*.java'
        include 'ucar/nc2/util/*.java'
        include 'ucar/unidata/**/*.java'
    }
}

task releaseDocs(dependsOn: javadoc) << {
    def releaseDir = webdir + "javadoc"

    ant.delete(dir: releaseDir)
    ant.mkdir(dir: releaseDir)

    copy {
        println "copyDocs to " + releaseDir
        from("build/docs/javadoc")
        into releaseDir
    }
}

//shadow {
//    outputFile = file("${destinationDir}/cdms-${version}.jar")
//    stats = true
//
//    exclude 'META-INF/*.DSA'
//    exclude 'META-INF/*.RSA'
//    exclude 'META-INF/NOTICE'
//    exclude 'META-INF/LICENSE'
//    exclude 'META-INF/*.txt'
//    exclude 'META-INF/*.xml'
//
//    artifactSet {
//        exclude 'commons-logging:commons-logging:*'
//    }
//
//    jar {
//        manifest {
//            attributes 'Main-Class': 'ucar.nc2.NCdumpW'
//        }
//    }
//}

//////////////////////////////////////// Test artifacts ////////////////////////////////////////

// This technique was inspired by:
// https://softnoise.wordpress.com/2014/09/07/gradle-sub-project-test-dependencies-in-multi-project-builds/

configurations {
    // Define a custom configuration and fill it with our test code's runtime dependencies.
    testArtifacts//.extendsFrom testRuntime
}

task testArtifactsJar(type: Jar, dependsOn: testClasses,
        group: 'build', description: 'Assembles a JAR containing the test classes.') {
    from sourceSets.test.output  // Our test classes.
    classifier = 'test'
}

artifacts {
    // Add the result of the "testArtifacts" TASK (i.e. cdm-<version>-test.jar) to the "testArtifacts" CONFIGURATION.
    // The config already contains the deps that our test classes require (added above in the configurations block).
    testArtifacts testArtifactsJar
    println testArtifactsJar.archivePath
}

tasks.check.doFirst {
    println configurations.getByName("testArtifacts").resolve().each {
        println it.getClass().toString() + ": " + it.toString() + "\n"
    }
}

// Other projects can depend on these artifacts using:
//     testCompile project(path: ':cdm', configuration: 'testArtifacts')
