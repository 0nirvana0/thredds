subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    version = '4.5.4'

    compileJava.options.encoding = 'UTF-8'

    // Unlike Maven, Gradle places CLASS files and resources in different directories by default.
    // This is a problem for IntelliJ: when running an application from the IDE, it expects everything to be in the
    // same directory. To fix, we're configuring resources to be copied to the same directory as CLASS files.
    processResources.destinationDir = compileJava.destinationDir

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata"
        }
        maven {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-3rdparty/"
        }
    }

    ext {
        // Dependency versions
        jfreechartVersion = "1.0.19"
        junitVersion = "4.12"
        log4jVersion = "2.0-rc1"
        servletVersion = "3.1.0"
        slf4jVersion = "1.7.7"
        springVersion = "3.2.12.RELEASE"
        jcommanderVersion = "1.35"
        jnaVersion = "4.1.0"
        commonsLang3Version = "3.3.2"
        commonsIoVersion = "2.4"
    }

    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
        }
    }

    jar {
        manifest {
            attributes 'Implementation-Version': version,
                    'Implementation-Vendor': 'UCAR/Unidata',
                    'Implementation-Vendor-Id': 'edu.ucar',
                    'Built-On': new Date(),
                    'Build-Jdk': System.properties['java.version'],
                    'Codebase': 'http://www.unidata.ucar.edu/software/thredds/',
                    'Permissions': 'all-permissions',
                    'Trusted-Only': 'true'
        }
    }

    dependencies {
        // testCompile project(':cdm').sourceSets.test.classes
        testCompile group: 'junit', name: 'junit', version: junitVersion
    }

    javadoc {
        options.encoding = 'UTF-8'
        // See http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
