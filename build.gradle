allprojects {
    group = "edu.ucar"
    version = '4.6.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    // Unlike Maven, Gradle places CLASS files and resources in different directories by default.
    // This is a problem for IntelliJ: when running an application from the IDE, it expects everything to be in the
    // same directory. To fix, we're configuring resources to be copied to the same directory as CLASS files.
    processResources.destinationDir = compileJava.destinationDir

    // TODO: Add to dependencies.gradle.
    repositories {
        mavenCentral()
        maven {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata"
        }
        maven {
            url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-3rdparty/"
        }
    }

    // TODO: Only cdm, ncIdv, tdm, ui, and visad use the shadow plugin, so we shouldn't be adding it as
    // a buildscript-dependency to all subprojects. We should break out the common stuff to /gradle/shadow.gradle
    // and then apply that script only to those 5.
    buildscript {
        repositories {
            jcenter()
        }
        dependencies {
            // Also, this needs to be declared in /gradle/dependencies.gradle
            classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
        }
    }

    // Will apply to "compileJava", "compileTestJava", "compileSourceSetJava", etc.
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'

        // Disable warnings about cross-compilation:
        // http://docs.oracle.com/javase/8/docs/technotes/tools/windows/javac.html#BHCIJIEG
        // This is usually fine, but see: http://www.draconianoverlord.com/2014/04/01/jdk-compatibility.html
        // To be safe, we should make sure that our production releases are built
        // on a machine running the same version of the JVM as our project language level.
        options.compilerArgs = ['-Xlint:-options']
    }

    // FIXME: Extend these to all Jar subtypes (e.g. War)?
    //Could I then add additional attributes in subproject build scripts?
    jar {
        manifest {
            attributes 'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor-Id': project.group,
                    'Implementation-Vendor': 'UCAR/Unidata',
                    // Commented out to avoid unnecessary re-compilation and re-jarring: http://goo.gl/kbVWXR
                    // I think I can fix this by writing a builtOn.txt file in compileJava,
                    // and then reading in its value here.
                    // 'Built-On': new Date(),
                    'Build-Jdk': JavaVersion.current(),
                    'Codebase': 'http://www.unidata.ucar.edu/software/thredds/',
                    'Permissions': 'all-permissions',
                    'Trusted-Only': 'true'
        }
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'

        if (JavaVersion.current().isJava8Compatible()) {
            // See http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

// Pulls contents into this script. Subprojects inherit the properties and methods defined by their parent.
apply from: "gradle/dependencies.gradle"

apply from: "gradle/publishing.gradle"

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
