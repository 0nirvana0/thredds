ext.title = "THREDDS Data Manager (TDM)"

dependencies {
    compile project(':httpservices')
    compile project(':cdm')
    compile project(':grib')
    compile project(':tdcommon')

    compile libraries["httpclient"]
    compile libraries["jdom2"]
    compile libraries["spring-core"]
    compile libraries["spring-context"]

    compile libraries["slf4j-api"]
    runtime libraries["log4j-slf4j-impl"]
    runtime libraries["log4j-core"]
}

jar.manifest.attributes 'Main-Class': 'thredds.tdm.Tdm'

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    archiveName = "tdm-4.6.jar"

    append('META-INF/spring.handlers')
    append('META-INF/spring.schemas')

    // Filter out crap from various other packages.
    exclude 'AUTHORS'
    exclude 'DATE'
    exclude 'LICENCE'
    exclude 'LICENSE'
    exclude 'NOTICE'
    exclude '*.txt'
    exclude '*.xml'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.txt'
    exclude 'META-INF/*.xml'
}

task showClasspath << {
    buildscript.configurations.classpath.each { println it }
}

task showMeCache << {
    configurations.compile.each { println it }
}
